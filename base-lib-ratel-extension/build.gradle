repositories {
    repositories {
        maven {
            name "aliyunmaven"
            url "https://maven.aliyun.com/repository/public"
        }
        maven {
            name "aliyunGoogle"
            url "https://maven.aliyun.com/repository/google"
        }
        maven {
            name "contralSnapshot"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'com.google.android:android:4.1.1.4'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'com.android.support:support-annotations:28.0.0'
    compileOnly 'com.virjar:ratel-api:1.3.6'
    compileOnly 'com.virjar:sekiro-api:1.0.3'
}

def JDK_VERSION = 1.7

sourceCompatibility = JDK_VERSION
targetCompatibility = JDK_VERSION


group 'com.virjar'
version '1.0.6'


compileJava {
    sourceCompatibility = JDK_VERSION
    targetCompatibility = JDK_VERSION
    [compileJava]*.options*.encoding = 'UTF-8'
}

compileTestJava {
    sourceCompatibility = JDK_VERSION
    targetCompatibility = JDK_VERSION
    [compileTestJava]*.options*.encoding = 'UTF-8'
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}
// 生成 javadoc jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
// javadoc 配置，这里是自定义了 java doc 的一些配置
javadoc {
    description = "Generates project-level javadoc for use in -javadoc jar"

    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.header = project.name
    options.addStringOption('Xdoclint:none', '-quiet')

    // suppress warnings due to cross-module @see and @link references;
    // note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
    options.encoding = "UTF-8"
    options.charSet = 'UTF-8'
}

def getRepositoryUsername() {
    return hasProperty('MAVEN_USERNAME') ? MAVEN_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('MAVEN_PASSWORD') ? MAVEN_PASSWORD : ""
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'ratel-extersion'
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar


            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "Ratel API"
                description = "a extension for ratel framework, support more reverse engineering tools based on ratel"
                url = "https://git.virjar.com:ratel/ratel"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "weijia.deng"
                        name = "dengweijia"
                        email = "virjar@virjar.com"
                    }
                }
                scm {
                    connection = "scm:git@git.virjar.com:ratel/ratel.git"
                    developerConnection = "scm:git@git.virjar.com:ratel/ratel.git"
                    url = "http://git.virjar.com:ratel/ratel"
                }
            }
        }
    }
    repositories {
        // releases 仓库
        maven {
            // 在对 task 中会生成对应的名称 publishMavenJavaPublicationToxxx
            // 后面的 xxx 就是你这里的名称，表示你要把jar 上传到这个仓库中
            name 'sonatypeRepository'  // 为你这个仓库起名
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = getRepositoryUsername()  // 之前在 sonatype 注册的账户名
                password = getRepositoryPassword() // 对应的密码
            }
        }
        // snapshots 仓库
        maven {
            name = 'sonatypeSnapshotRepository'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
    }
}
// 签名配置，注意这里的顺序，今天第一次知道 gradle 中的 task 等配置也是有顺序的
// 必须在 publishing 配置之后
signing {
    sign publishing.publications.mavenJava
}